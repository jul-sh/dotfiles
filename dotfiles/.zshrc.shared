# Source .profile if it exists
if [ -f "${HOME}/.profile" ]; then
  source "${HOME}/.profile"
fi

# Print a greeting message
echo -e "\e[3mHi girl, you're doing great this $(date +"%A"). —ฅ/ᐠ. ̫.ᐟ\ฅ—\e[0m"

# --------------------------------
# ZSH Other Configuration
# --------------------------------

# Show hidden files in completions
setopt globdots

# Don't error on comments in shell
setopt interactivecomments

# Source plugins from Nix profiles (no git cloning)
source_nix_plugin() {
  local plugin_name="$1"
  local relative_path="$2"
  local profiles=()

  # NIX_PROFILES is space-separated; split into an array when set
  if [ -n "${NIX_PROFILES:-}" ]; then
    profiles=("${(@s: :)NIX_PROFILES}")
  fi

  # Fallbacks for common profile locations
  profiles+=("$HOME/.nix-profile" "/nix/var/nix/profiles/default")

  for profile in "${profiles[@]}"; do
    local candidate="${profile}/share/${relative_path}"
    if [ -f "$candidate" ]; then
      source "$candidate"
      return 0
    fi
  done

  echo "⚠️  ${plugin_name} not found in active Nix profiles. Install it via Nix to enable the plugin." >&2
  return 1
}

# Initialize completion system
autoload -Uz compinit
# Avoid recompiling .zcompdump too often, check if it's older than 1 day
if [[ ! -f ~/.zcompdump || -z $(find ~/.zcompdump -mtime -1) ]]; then
  compinit -d ~/.zcompdump # Specify dump file path
else
  compinit -i -d ~/.zcompdump # Use existing dump file without checks
fi

source_nix_plugin "zsh-syntax-highlighting" "zsh-syntax-highlighting/zsh-syntax-highlighting.zsh"

if source_nix_plugin "zsh-autocomplete" "zsh-autocomplete/zsh-autocomplete.plugin.zsh"; then
  bindkey '^I' menu-select
  bindkey -M menuselect '^I' menu-complete
  zstyle ':autocomplete:tab:*' widget-style menu-complete
  bindkey -M menuselect "$terminfo[kcbt]" reverse-menu-complete
  zstyle ':autocomplete:*' min-input 3
  # Don't prompt for confirmation if there's many completions to show
  zstyle ':completion:*' menu yes select
  # Esc to exit autocomplete menu
  bindkey -M menuselect '^[' undo
  # Restore default history binding, otherwise occupied by zsh autocomplete
  bindkey -M emacs \
      "^[p"   .history-search-backward \
      "^[n"   .history-search-forward \
      "^P"    .up-line-or-history \
      "^[OA"  .up-line-or-history \
      "^[[A"  .up-line-or-history \
      "^N"    .down-line-or-history \
      "^[OB"  .down-line-or-history \
      "^[[B"  .down-line-or-history \
      "^R"    .history-incremental-search-backward \
      "^S"    .history-incremental-search-forward \
      #
  bindkey -a \
      "^P"    .up-history \
      "^N"    .down-history \
      "k"     .up-line-or-history \
      "^[OA"  .up-line-or-history \
      "^[[A"  .up-line-or-history \
      "j"     .down-line-or-history \
      "^[OB"  .down-line-or-history \
      "^[[B"  .down-line-or-history \
      "/"     .vi-history-search-backward \
      "?"     .vi-history-search-forward \
      #
fi

# Use atuin for history search
eval "$(atuin init zsh)"

# Check for dotfile updates
if [ -f "${HOME}/.dotfiles_update.sh" ]; then
  source "${HOME}/.dotfiles_update.sh"
fi

if [ -f "${HOME}/.utils.sh" ]; then
  source "${HOME}/.utils.sh"
fi

# Disable Ctrl+D to close session
setopt ignoreeof


# Initialize starship prompt
eval "$(starship init zsh)"
