# Print a greeting message
echo -e "\e[3mHi girl, you're doing great this $(date +"%A"). —ฅ/ᐠ. ̫.ᐟ\ฅ—\e[0m"

# --------------------------------
# ZSH Other Configuration
# --------------------------------

# Show hidden files in completions
setopt globdots

# Don't error on comments in shell
setopt interactivecomments

# Disable Ctrl+D to close session
setopt ignoreeof

# --------------------------------
# ZSH Plugins (Setup by Nix in home.nix)
# --------------------------------

# Load Nix-managed plugins (syntax highlighting, autocomplete)
if [ -f "$HOME/.zsh_plugins.sh" ]; then
  source "$HOME/.zsh_plugins.sh"
fi

# Initialize completion system
autoload -Uz compinit
# Avoid recompiling .zcompdump too often, check if it's older than 1 day
if [[ ! -f ~/.zcompdump || -z $(find ~/.zcompdump -mtime -1) ]]; then
  compinit -d ~/.zcompdump # Specify dump file path
else
  compinit -i -d ~/.zcompdump # Use existing dump file without checks
fi

# Autocomplete keybindings
bindkey '^I' menu-select
bindkey -M menuselect '^I' menu-complete
zstyle ':autocomplete:tab:*' widget-style menu-complete
bindkey -M menuselect "$terminfo[kcbt]" reverse-menu-complete
zstyle ':autocomplete:*' min-input 3
# Don't prompt for confirmation if there's many completions to show
zstyle ':completion:*' menu yes select
# Esc to exit autocomplete menu
bindkey -M menuselect '^[' undo
# Restore default history binding, otherwise occupied by zsh autocomplete
bindkey -M emacs \
    "^[p"   .history-search-backward \
    "^[n"   .history-search-forward \
    "^P"    .up-line-or-history \
    "^[OA"  .up-line-or-history \
    "^[[A"  .up-line-or-history \
    "^N"    .down-line-or-history \
    "^[OB"  .down-line-or-history \
    "^[[B"  .down-line-or-history \
    "^R"    .history-incremental-search-backward \
    "^S"    .history-incremental-search-forward \
    #
bindkey -a \
    "^P"    .up-history \
    "^N"    .down-history \
    "k"     .up-line-or-history \
    "^[OA"  .up-line-or-history \
    "^[[A"  .up-line-or-history \
    "j"     .down-line-or-history \
    "^[OB"  .down-line-or-history \
    "^[[B"  .down-line-or-history \
    "/"     .vi-history-search-backward \
    "?"     .vi-history-search-forward \
    #

# Use atuin for history search
if command -v atuin &> /dev/null; then
  eval "$(atuin init zsh)"
fi


# Initialize direnv for zsh
if command -v direnv &> /dev/null; then
  eval "$(direnv hook zsh)"
fi

# Initialize starship prompt
if command -v starship &> /dev/null; then
  eval "$(starship init zsh)"
fi

attach() {
    # 1. Determine the target name
    if [ -n "$1" ]; then
        TARGET="$1"
    else
        DIR_NAME=$(basename "$PWD")
        PATH_HASH=$(echo "$PWD" | md5sum | cut -c1-4)
        TARGET="${DIR_NAME}_${PATH_HASH}"
    fi

    # 2. Check for an EXACT match first
    if zellij list-sessions 2>/dev/null | grep -q "^$TARGET "; then
        zellij attach "$TARGET"
        return
    fi

    # 3. Check for a FUZZY match (e.g., "clipkitty" matches "clipkitty_1234")
    # We take the first match found in the list
    FUZZY_MATCH=$(zellij list-sessions 2>/dev/null | grep "^$TARGET\_" | head -n 1 | awk '{print $1}')

    if [ -n "$FUZZY_MATCH" ]; then
        echo "Found existing session: $FUZZY_MATCH"
        zellij attach "$FUZZY_MATCH"
    else
        # 4. If nothing found, create new
        echo "Starting new session: $TARGET"
        zellij -s "$TARGET"
    fi
}
